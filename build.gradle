description = 'Parent project for all Cedexis Java apps'

buildscript {
    repositories {
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = 'GitHub'
            addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
        }
    }
    dependencies {
        def tomcatPluginVersion = '0.9.2'
        classpath "bmuschko:gradle-tomcat-plugin:${tomcatPluginVersion}"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'project-report'
    apply plugin: 'eclipse'
    apply plugin: 'checkstyle'
    
    sourceCompatibility = 1.6
    
    version = '0.1'
    
    repositories {
        mavenCentral()
    }
    
    configurations {
        tomcatLibs
    }
    
    dependencies {
        compile 'com.google.inject:guice:3.0'
        compile 'com.google.guava:guava:12.0.1'
        testCompile 'junit:junit:4.8.1'
        tomcatLibs 'javax.servlet:javax.servlet-api:3.0.1'
        tomcatLibs 'javax.servlet.jsp:jsp-api:2.2'
    }
    
    eclipse {
        classpath {
            // Gradle defaults main classes dir to /bin, make it build/classes/main
            defaultOutputDir = sourceSets.main.output.classesDir
            file {
                whenMerged { cp ->
                    // Gradle defaults test classes dir to /bin, make it build/classes/test
                    cp.entries.findAll { it.kind == 'src' && it.path.startsWith("src/test/") }*.output = project.relativePath(sourceSets.test.output.classesDir)
                }
            }
        }
    }

    // completely overwrite existing Eclipse files
    tasks.eclipse.dependsOn(cleanEclipse)
}